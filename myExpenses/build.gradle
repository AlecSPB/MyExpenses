import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.totschnig.myexpenses"
        minSdkVersion 8
        targetSdkVersion 19
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        testApplicationId "org.totschnig.myexpenses.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }
    lintOptions {
        disable 'InvalidPackage', 'MissingQuantity', 'MissingTranslation', 'ImpliedQuantity'
    }
}

dependencies {
    compile project(':StickyListHeaders:library')
    compile project(':MPAndroidChart:MPChartLib')
    compile project(':PlayLicensingOrig')
    compile 'com.android.support:appcompat-v7:19.0.0'
    compile 'com.itextpdf:itextpdf:5.5.2'
    compile 'ch.acra:acra:4.5.0'
    compile 'org.onepf:openiab:0.9.7.2' //TODO upgrade

    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
}
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HHmm')
    return formattedDate
}
task copyTask(type: Copy) {
    from 'template/app.properties'
    into 'src/main/res/raw'
    filter(ReplaceTokens, tokens: ["build.date": getDate()])
}
project.afterEvaluate{
    compileDebugJava.dependsOn("copyTask")
    compileReleaseJava.dependsOn("copyTask")
}
